//
// Qt4xHb Project - Test Program
//
// Copyright (c) 2024 Marcos Antonio Gambeta <marcosgambeta AT outlook DOT com>
//

// Exemplo de criação da janela principal de uma aplicação

#include "qt4xhb.ch"

STATIC oApp
STATIC oMainWindow

// a função principal (main) cria um objeto da classe QApplication,
// um objeto da classe HMainWindow e inicia o loop de eventos

PROCEDURE Main()

   oApp := QApplication():new()

   oMainWindow := HMainWindow():new()

   oMainWindow:show()

   oApp:exec()

   oMainWindow:delete()

   oApp:delete()

RETURN

// a classe HMainWindow é uma classe derivada da classe QMainWindow, com métodos para:
// . definir as propriedades da janela
// . criar a barra de menus e as ações
// . criar a barra de ferramentas
// . criar a barra de status
// . confirmar se o usuário quer realmente sair do programa
// . exibir mensagens

#include "hbclass.ch"

CLASS HMainWindow INHERIT QMainWindow

   DATA oMenuBar
   DATA oMenu1
   DATA oActionNew
   DATA oActionOpen
   DATA oActionSave
   DATA oMenu2
   DATA oActionCut
   DATA oActionCopy
   DATA oActionPaste
   DATA oMenu3
   DATA oActionAbout
   DATA oActionAboutxH
   DATA oActionAboutCC
   DATA oActionAboutQt
   DATA oToolBar
   DATA oStatusBar

   METHOD new
   METHOD defineProperties
   METHOD createMenuBar
   METHOD createToolBar
   METHOD createStatusBar
   METHOD defineEvents
   METHOD closeMainWindow
   METHOD showMessage

END CLASS

// o método 'new' executa o método 'new' da classe QMainWindow
// e configura a janela principal

METHOD new() CLASS HMainWindow

   #ifdef __XHARBOUR__
   super:new()
   #else
   ::super:new()
   #endif

   ::defineProperties()
   ::createMenuBar()
   ::createToolBar()
   ::createStatusBar()
   ::defineEvents()

RETURN self

// o método 'defineProperties' define as propriedades
// 'title' e 'size' da janela principal

METHOD defineProperties() CLASS HMainWindow

   ::setWindowTitle("Qt4xHb - exemplo de criação da janela principal")
   ::resize(800, 600)

RETURN self

// o método 'createMenuBar' cria os menus da janela principal
// e configura a ação que cada item do menu irá executar

METHOD createMenuBar() CLASS HMainWindow

   ::oMenuBar := ::menuBar()

   ::oMenu1 := ::oMenuBar:addMenu("&Arquivo")

   ::oActionNew := ::oMenu1:AddAction(QIcon():new("images\new.png"), "&Novo")
   ::oActionNew:setStatusTip("Executa a opção NOVO")
   ::oActionNew:onTriggered({||::showMessage("Arquivo/Novo")})

   ::oActionOpen := ::oMenu1:AddAction(QIcon():new("images\open.png"), "&Abrir")
   ::oActionOpen:setStatusTip("Executa a opção ABRIR")
   ::oActionOpen:onTriggered({||::showMessage("Arquivo/Abrir")})

   ::oActionSave := ::oMenu1:AddAction(QIcon():new("images\save.png"), "&Salvar")
   ::oActionSave:setStatusTip("Executa a opção SALVAR")
   ::oActionSave:onTriggered({||::showMessage("Arquivo/Salvar")})

   ::oMenu2 := ::oMenuBar:AddMenu("&Editar")

   ::oActionCut := ::oMenu2:AddAction(QIcon():New("images\cut.png"), "&Recortar")
   ::oActionCut:setStatusTip("Executa a opção RECORTAR")
   ::oActionCut:onTriggered({||::showMessage("Editar/Recortar")})

   ::oActionCopy := ::oMenu2:AddAction(QIcon():new("images\copy.png"), "&Copiar")
   ::oActionCopy:setStatusTip("Executa a opção COPIAR")
   ::oActionCopy:onTriggered({||::showMessage("Editar/Copiar")})

   ::oActionPaste := ::oMenu2:AddAction(QIcon():New("images\paste.png"), "C&olar")
   ::oActionPaste:setStatusTip("Executa a opção COLAR")
   ::oActionPaste:onTriggered({||::showMessage("Editar/Colar")})

   ::oMenu3 := ::oMenuBar:AddMenu("&Sobre")

   ::oActionAbout := ::oMenu3:AddAction("&Exemplo")
   ::oActionAbout:setStatusTip("Exibe informações sobre este exemplo")
   ::oActionAbout:onTriggered({||::showMessage("Este exemplo demonstra a criação da janela principal de uma aplicação")})

   ::oActionAboutxH := ::oMenu3:AddAction("&xHarbour")
   ::oActionAboutxH:setStatusTip("Exibe a versão do Harbour/xHarbour")
   ::oActionAboutxH:onTriggered({||::showMessage(version())})

   ::oActionAboutCC := ::oMenu3:AddAction("&C/C++ Compiler")
   ::oActionAboutCC:setStatusTip("Exibe informações sobre o compilador C/C++")
   ::oActionAboutCC:onTriggered({||::showMessage(hb_compiler())})

   ::oActionAboutQt := ::oMenu3:AddAction("&Qt Framework")
   ::oActionAboutQt:setStatusTip("Exibe informações sobre o Qt Framework")
   ::oActionAboutQt:onTriggered({||oApp:aboutQt()})

RETURN self

// o método 'createToolBar' cria a barra de ferramentas da janela principal
// e configura a ação que cada botão irá executar

METHOD createToolBar() CLASS HMainWindow

   ::oToolBar := ::addToolBar("")

   ::oToolBar:addAction(::oActionNew)
   ::oToolBar:addAction(::oActionOpen)
   ::oToolBar:addAction(::oActionSave)

   ::oToolBar:addSeparator()

   ::oToolBar:addAction(::oActionCut)
   ::oToolBar:addAction(::oActionCopy)
   ::oToolBar:addAction(::oActionPaste)

RETURN self

// o método 'createStatusBar' cria a barra de status

METHOD createStatusBar() CLASS HMainWindow

   ::oStatusBar := ::statusBar()

RETURN self

// o método 'defineEvent' define os eventos que serão processados

METHOD defineEvents() CLASS HMainWindow

   ::onCloseEvent({|oSender, oEvent|::CloseMainWindow(oSender, oEvent)})

RETURN self

// o método 'closeMainWindow' confirma, com o usuário, se quer
// realmente sair do programa

METHOD closeMainWindow(oSender, oCloseEvent) CLASS HMainWindow

   LOCAL oMB
   LOCAL nRet

   oMB := QMessageBox():new(QMessageBox_Question, ;
                             "Atenção", ;
                             "Quer realmente sair do programa ?", ;
                             QMessageBox_Yes + QMessageBox_No, ;
                             self, ;
                             Qt_Dialog + Qt_MSWindowsFixedSizeDialogHint)

   nRet := oMB:exec()

   oMB:delete()

   IF nRet == QMessageBox_Yes
      oCloseEvent:accept()
   endif

   IF nRet == QMessageBox_No
      oCloseEvent:ignore()
   endif

RETURN .T.

// o método 'showMessage' exibe uma janela com uma mensagem,
// usando a classe QMessageBox

METHOD showMessage(cText) CLASS HMainWindow

   LOCAL oMB

   oMB := QMessageBox():new(QMessageBox_Information, "Informação", cText, QMessageBox_Ok, self)

   oMB:exec()

   oMB:delete()

RETURN NIL
