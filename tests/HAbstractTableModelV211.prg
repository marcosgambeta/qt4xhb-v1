//
// Qt4xHb Project - Test Program
//
// Copyright (c) 2025 Marcos Antonio Gambeta <marcosgambeta@outlook.com>
//

#include "qt4xhb.ch"

STATIC s_aEstados

FUNCTION Main()

   LOCAL oApp
   LOCAL oWindow
   LOCAL oModel
   LOCAL oView

   // create application
   oApp := QApplication():new()

   // create window
   oWindow := QWidget():new()
   oWindow:setWindowTitle("Test with HAbstractTableModelV2 class")
   oWindow:resize(800, 600)

   s_aEstados := {}
   aadd(s_aEstados, {"images\estados\acre.png"            , "Acre"               })
   aadd(s_aEstados, {"images\estados\alagoas.png"         , "Alagoas"            })
   aadd(s_aEstados, {"images\estados\amapa.png"           , "Amapá"              })
   aadd(s_aEstados, {"images\estados\amazonas.png"        , "Amazonas"           })
   aadd(s_aEstados, {"images\estados\bahia.png"           , "Bahia"              })
   aadd(s_aEstados, {"images\estados\ceara.png"           , "Ceará"              })
   aadd(s_aEstados, {"images\estados\distritofederal.png" , "Distrito Federal"   })
   aadd(s_aEstados, {"images\estados\espiritosanto.png"   , "Espírito Santo"     })
   aadd(s_aEstados, {"images\estados\goias.png"           , "Goiás"              })
   aadd(s_aEstados, {"images\estados\maranhao.png"        , "Maranhão"           })
   aadd(s_aEstados, {"images\estados\matogrosso.png"      , "Mato Grosso"        })
   aadd(s_aEstados, {"images\estados\matogrossodosul.png" , "Mato Grosso do Sul" })
   aadd(s_aEstados, {"images\estados\minasgerais.png"     , "Minas Gerais"       })
   aadd(s_aEstados, {"images\estados\para.png"            , "Pará"               })
   aadd(s_aEstados, {"images\estados\paraiba.png"         , "Paraíba"            })
   aadd(s_aEstados, {"images\estados\parana.png"          , "Paraná"             })
   aadd(s_aEstados, {"images\estados\pernambuco.png"      , "Pernambuco"         })
   aadd(s_aEstados, {"images\estados\piaui.png"           , "Piauí"              })
   aadd(s_aEstados, {"images\estados\riodejaneiro.png"    , "Rio de Janeiro"     })
   aadd(s_aEstados, {"images\estados\riograndedonorte.png", "Rio Grande do Norte"})
   aadd(s_aEstados, {"images\estados\riograndedosul.png"  , "Rio Grande do Sul"  })
   aadd(s_aEstados, {"images\estados\rondonia.png"        , "Rondônia"           })
   aadd(s_aEstados, {"images\estados\roraima.png"         , "Roraima"            })
   aadd(s_aEstados, {"images\estados\santacatarina.png"   , "Santa Catarina"     })
   aadd(s_aEstados, {"images\estados\saopaulo.png"        , "São Paulo"          })
   aadd(s_aEstados, {"images\estados\sergipe.png"         , "Sergipe"            })
   aadd(s_aEstados, {"images\estados\tocantins.png"       , "Tocantins"          })

   // create model
   oModel := myModel():new()

   // create view
   oView := QTableView():new(oWindow)
   oView:move(10, 10)
   oView:resize(800 - 20, 600 - 20)
   oView:setModel(oModel)
   oView:resizeRowsToContents()
   oView:resizeColumnsToContents()

   // show window
   oWindow:show()

   // start application
   oApp:exec()

   // delete objects
   oModel:delete()
   oWindow:delete()
   oApp:delete()

RETURN NIL

#include <hbclass.ch>

CLASS myModel INHERIT HAbstractTableModelV2

   METHOD new
   METHOD rowCount
   METHOD columnCount
   METHOD data
   METHOD headerData

END CLASS

METHOD new(...) CLASS myModel

   ::super:new(...)

   ::setRowCountCB({|...|::rowCount(...)})
   ::setColumnCountCB({|...|::columnCount(...)})
   ::setDataCB({|...|::data(...)})
   ::setHeaderDataCB({|...|::headerData(...)})

RETURN self

METHOD rowCount() CLASS myModel
RETURN len(s_aEstados)

METHOD columnCount() CLASS myModel
RETURN 2 // coluna 1=imagem coluna 2=nome

METHOD data(oIndex, nRole) CLASS myModel

   LOCAL oVariant := QVariant():new()
   LOCAL nRow := oIndex:row()
   LOCAL nColumn := oIndex:column()

   IF oIndex:isValid()

      IF nRole == Qt_DisplayRole .AND. nColumn == 1
         oVariant:setValue(s_aEstados[nRow + 1, 2])
      ELSEIF nRole == Qt_DecorationRole .AND. nColumn == 0
         oVariant := QVariant():new(QPixmap():new(s_aEstados[nRow + 1, 1]):toVariant())
      ELSEIF nRole == Qt_SizeHintRole .AND. nColumn == 0
         oVariant:setValue(QSize():new(150, 107))
      ENDIF

   ENDIF

RETURN oVariant

METHOD headerData(nSection, nOrientation, nRole) CLASS myModel

   LOCAL oVariant := QVariant():new()

   IF nOrientation == Qt_Horizontal .AND. nRole == Qt_DisplayRole
      oVariant:setValue({"Bandeira", "Estado"}[nSection + 1])
   ELSEIF nOrientation == Qt_Vertical .AND. nRole == Qt_DisplayRole
      oVariant:setValue("Row " + alltrim(str(nSection)))
   ENDIF

RETURN oVariant
